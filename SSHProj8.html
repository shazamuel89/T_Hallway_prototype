<!DOCTYPE html>
<html>
    <head>
        <title>Samuel's Project</title>
    </head>
    <body style="background-color: black">
        <div  style="color: white">
            FOV: <input id="fovySlider" type="range" min="10" max="120" step="1" value="60"/>
            <span id="fovyValue">60</span>
        </div>
        <canvas id="gl-canvas" width="700" height="700">
            Oops... your browser doesn't support the HTML5 canvas element.
        </canvas>
    </body>
    <script id="vertex-shader" type="x-shader/x-vertex">#version 300 es
        in vec4 aPosition;
        in vec3 aNormal;
        in vec2 aTexCoord;

        uniform float uDisplacementX;
        uniform float uDisplacementY;
        uniform float uDisplacementZ;
        uniform mat4 uProjectionMatrix;
        uniform vec4 uLightPosition;

        out vec3 L; // Direction from vertex to light source
        out vec3 E; // Direction from vertex to camera
        out vec3 N; // Direction of vertex normal
        out vec2 vTexCoord;

        void main() {
            mat4 translationMatrix = mat4(1.0,            0.0,            0.0,            0.0,
                                          0.0,            1.0,            0.0,            0.0,
                                          0.0,            0.0,            1.0,            0.0,
                                          uDisplacementX, uDisplacementY, uDisplacementZ, 1.0);
            
            mat4 modelViewMatrix = mat4(1.0); // Declare model view matrix as identity matrix
            modelViewMatrix = translationMatrix * modelViewMatrix; // Apply translation to model
            vec3 position = (modelViewMatrix * aPosition).xyz; // Get position of vertex
            vec3 light = uLightPosition.xyz; // Get position of light source in clip space
            L = normalize(light - position);
            E = -position; // Negative because switching from pointing from viewer to vertex to pointing from vertex to viewer
            N = normalize((modelViewMatrix * vec4(aNormal, 0.0)).xyz); // Get normal in eye space

            gl_Position = uProjectionMatrix * modelViewMatrix * aPosition;
            vTexCoord = aTexCoord;
        }
    </script>
    <script id="fragment-shader" type="x-shader/x-fragment">#version 300 es
        precision mediump float;

        in vec3 L; // Direction from fragment to light
        in vec3 E; // Direction from fragment to eye
        in vec3 N; // Direction of fragment's normal
        in vec2 vTexCoord;

        uniform vec4 uAmbientProduct;
        uniform vec4 uDiffuseProduct;
        uniform vec4 uSpecularProduct;
        uniform float uShininess;
        uniform sampler2D uTex0;

        out vec4 fColor;

        void main() {
            vec3 H = normalize(L + E); // Get direction of halfway vector between light and eye direction
            
            vec4 ambient = uAmbientProduct; // Ambient light is independent of location
            
            float Kd = max(dot(L, N), 0.0); // Get diffuse coefficient
            vec4 diffuse = Kd * uDiffuseProduct; // Calculate diffuse component of lighting

            float Ks = pow(max(dot(N, H), 0.0), uShininess); // Get specular coefficient
            vec4 specular = Ks * uSpecularProduct; // Calculate specular component of lighting

            if (dot(L, N) < 0.0) {
                specular = vec4(0.0, 0.0, 0.0, 1.0);
            }

            vec4 lighting = ambient + diffuse + specular; // Calculate color contribution of all lighting

            vec4 combinedTexture = texture(uTex0, vTexCoord);
            vec3 lightColor = combinedTexture.rgb * lighting.rgb; // Apply lighting to RGB channels onto textures
            fColor = vec4(lightColor, combinedTexture.a); // Calculate color using light.rgb and tex.a
        }
    </script>
    <script type="text/javascript" src="initShaders.js"></script>
    <script type="text/javascript" src="MV.js"></script>
    <script type="text/javascript" src="SSHProj8.js"></script>
</html>